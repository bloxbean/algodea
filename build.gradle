import com.vladsch.flexmark.ast.Document
import com.vladsch.flexmark.html.HtmlRenderer
import com.vladsch.flexmark.parser.Parser

buildscript {
    apply plugin: 'groovy'

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.vladsch.flexmark', name: 'flexmark', version: '0.28.12'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '1.3.0'
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
}

apply plugin: "kotlin"

group 'com.bloxbean'
version '0.9.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'lib')
    compile group: 'com.algorand', name: 'algosdk', version: '1.10.0'
    compile('com.github.kklisura.cdt:cdt-java-client:4.0.0')  {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets.main.java.srcDirs('src/main/gen', 'src/main/idea')

String changeLogAsHtml() {
    Parser parser = Parser.builder().build()
    HtmlRenderer renderer = HtmlRenderer.builder().build()
    Document changeLogDocument = parser.parse(new File(rootProject.uri('CHANGELOG.md')).text)
    renderer.render(changeLogDocument)
}

intellij {
    version = '2021.1'
    //type = 'IC'
    plugins = ['java']

    updateSinceUntilBuild = false
}
patchPluginXml {
    changeNotes = changeLogAsHtml()
}
