import com.vladsch.flexmark.ast.Document
import com.vladsch.flexmark.html.HtmlRenderer
import com.vladsch.flexmark.parser.Parser

buildscript {
    apply plugin: 'groovy'

    repositories {
        mavenLocal()
        mavenCentral()
        maven{
            url 'https://jetbrains.bintray.com/intellij-plugin-service'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.vladsch.flexmark', name: 'flexmark', version: '0.28.12'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.6.5'
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
}

apply plugin: "kotlin"
apply plugin: "org.jetbrains.intellij"

group 'com.bloxbean'
version '0.3.4-beta1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'lib')
    compile group: 'com.algorand', name: 'algosdk', version: '1.5.1'

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.10")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

sourceSets.main.java.srcDirs('src/main/gen', 'src/main/idea')
//sourceSets.main.java.srcDirs('src/main/gen')

String changeLogAsHtml() {
    Parser parser = Parser.builder().build()
    HtmlRenderer renderer = HtmlRenderer.builder().build()
    Document changeLogDocument = parser.parse(new File(rootProject.uri('CHANGELOG.md')).text)
    renderer.render(changeLogDocument)
}

intellij {
    version '2020.2'
//    type = 'PY'
    plugins = ['java']

    publishPlugin {
        username System.getProperty('ij_plugin_portal_login')
        password System.getProperty('ij_plugin_portal_password')
    }

    updateSinceUntilBuild false
}
patchPluginXml {
    changeNotes changeLogAsHtml()
}
